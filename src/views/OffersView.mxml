<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark"
		skinClass="skins.BJSkin"
		title="Offers"
		actionBarVisible="false"
		updateComplete="offersView_updateCompleteHandler(event)"
		viewActivate="offersView_viewActivateHandler(event)">
	
	<s:layout>
		<s:VerticalLayout gap="0"/>
	</s:layout>
	
	<fx:Metadata>
		[ResourceBundle("resources")]
	</fx:Metadata> 
	
	<fx:Script>
		<![CDATA[
			import events.ViewEvent;
			
			import models.JobOfferModel;
			
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			import mx.utils.object_proxy;
			
			import spark.events.IndexChangeEvent;
			import spark.events.ViewNavigatorEvent;
			
			import utilities.AppSettings;
			import utilities.Funcs;
			
			private var superViewJobs:SearchJobsView;
			private var superViewPeople:SearchPeopleView;
			private var humanYn:Boolean = false;
			
			[Bindable]
			private var offersModel:JobOfferModel;
			
			protected function offersView_updateCompleteHandler(event:FlexEvent):void {
				if (navigator.poppedViewReturnedObject != null) {
					var data:Object = navigator.poppedViewReturnedObject;
					if (data != null && data.object != null && data.object.superView != null && data.object.resetBar != null) {
						if (data.object.jobsYn) {
							this.superViewJobs = data.object.superView as views.SearchJobsView;
							if (data.object.resetBar)
								this.superViewJobs.setBar();
							if (data.object.cid > 0)
								this.superViewJobs.title = resourceManager.getString('resources','menu_Jobs') + " - " + data.object.categorytitle;
						}
						else {
							this.superViewPeople = data.object.superView as views.SearchPeopleView;
							if (data.object.resetBar)
								this.superViewPeople.setBar();
							if (data.object.cid > 0)
								this.superViewPeople.title = resourceManager.getString('resources','menu_People') + " - " + data.object.categorytitle;
						}
						AppSettings.getInstance().CurrentCategoryID = data.object.cid;
					}
				}
				this.title = resourceManager.getString('resources','menu_Offers');
			}
			
			protected function offersView_viewActivateHandler(event:ViewNavigatorEvent):void {
				setNavData();
			}
			
			public function loadItems(event:Event):void {
				if (AppSettings.getInstance().dbHelper != null) {
					this.offersModel = JobOfferModel.getInstance();

					var offs:Array = null;
					if (AppSettings.getInstance().stShowCategories && AppSettings.getInstance().CurrentCategoryID > 0)
						offs = AppSettings.getInstance().dbHelper.getJobOffersComplex(this.humanYn, AppSettings.getInstance().CurrentCategoryID);
					else
						offs = AppSettings.getInstance().dbHelper.getJobOfferForHuman(this.humanYn);
	
					if (offs != null) {
						var f:Funcs = new Funcs();
						
						if (this.offersModel.items == null)
							this.offersModel.items = new ArrayCollection();
						else
							this.offersModel.items.removeAll();
	
						var off:Object, obj:Object;
						var today:Date = new Date();
						for (var i:int=0; i<offs.length; i++) {
							off = offs[i];
							off.listtitle = f.getFullDate(off.publishdate as Date, today, false) + " // " + off.title;
							this.offersModel.items.addItem(off);
						}
					}
					else
						this.offersModel.items.removeAll();

					AppSettings.getInstance().CurrentCategoryID = 0;
				}
			}

			private function setNavData():void {
				this.superViewJobs = null;
				this.superViewPeople = null;
				this.humanYn = true;
				if (this.parentDocument != null) {
					if (this.parentDocument.className == "SearchPeopleView")
						this.superViewPeople = this.parentDocument as SearchPeopleView;
					else {
						this.humanYn = false;
						this.superViewJobs = this.parentDocument as SearchJobsView;
					}
				}
				this.loadItems(null);
			}
			
			protected function listOffers_changeHandler(event:IndexChangeEvent):void {
				var o:Object = new Object();
				o.itemObject = this.listOffers.selectedItem;
				o.showActionBar = false;
				o.superView = ((this.superViewJobs == null) ? this.superViewPeople : this.superViewJobs);
				o.jobsYn = ((this.superViewJobs == null) ? false : true);
				navigator.pushView(OfferDetailsView, o);
			}
			
		]]>
	</fx:Script>
	
	<fx:Style>
		.nofItem { fontStyle: italic; color: #DF9368; }
	</fx:Style>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<s:List id="listOffers" width="100%" height="100%" dataProvider="{offersModel.items}"
			alternatingItemColors="[0xE3E3E3, 0xEBEBEC]"
			change="listOffers_changeHandler(event)">
		<s:itemRenderer>
			<fx:Component>
				<s:IconItemRenderer labelField="listtitle" messageField="categorytitle" messageStyleName="nofItem" />
			</fx:Component>
		</s:itemRenderer>
	</s:List>
	
</s:View>
